rule "backend card uid"
when
  contains(to_string($message.message), "Got card_uid")
then
  let expr = grok("(?s)Got card_uid = (?<card_uid>(%{INT}\\.?){4})", to_string($message.message));
  set_fields(expr);
end

###########

rule "extract IP"
when
  contains(to_string($message.filebeat_log_file_path), "access.log")
then
  set_fields(grok("%{IPORHOST:clientip} (?:-|(%{WORD}.%{WORD})) %{USER:ident} \\[%{HTTPDATE:timestamp}\\] \"(?:%{WORD:verb} %{NOTSPACE:request}(?: HTTP/%{NUMBER:httpversion})?|%{DATA:rawrequest})\" %{NUMBER:response} (?:%{NUMBER:bytes}|-) %{QS:referrer} %{QS:agent} %{QS:forwarder}", to_string($message.message)));
end

###########

rule "parse backend logs"
when
  contains(to_string($message.message), "####################")
then
  let expr = grok("(?s)%{TIMESTAMP_ISO8601:timestamp}%{SPACE}%{LOGLEVEL}%{GREEDYDATA:message}", to_string($message.message));
  set_fields(expr);
end

###########

rule "parse door command"
when
  starts_with(to_string($message.parsed_msg), "sending") &&
  contains(to_string($message.parsed_msg), "command")
then
  let res = regex("room=(\\d\\w?) command to (open|close)", to_string($message.parsed_msg));
  set_field("room", res["0"]);
  set_field("command", res["1"]);
  set_field("type", "door command");
end

rule "parse loguru logs"
when
true
then
let grok_pattern = "%{TIMESTAMP_ISO8601:timestamp} \\| %{LOGLEVEL}%{SPACE}\\| (?<module>(%{WORD}\\.?)+):%{WORD:function}:%{NUMBER:line_number} - %{GREEDYDATA:parsed_msg}";
set_fields(grok(pattern: grok_pattern, value: to_string($message.message)));
end

###########

rule "parse open request"
when
  contains(value: to_string($message.parsed_msg), search: "open request for")
then
  let reg = regex("open request for (\\d\\w) from (\\d+)", to_string($message.parsed_msg));
  set_field("room", reg["0"]);
  set_field("account_id", reg["1"]);
  set_field("type", "open request");
end

###########

rule "parse payment"
when
  contains(to_string($message.filebeat_log_file_path), "payment_events")
then
  let grok_expr = "(?s)%{TIMESTAMP_ISO8601:timestamp}%{SPACE}%{LOGLEVEL}:%{SPACE}- SUCCESS - for user %{NUMBER:user_id}%{GREEDYDATA}+%{NUMBER:add_money} = %{NUMBER:total_money}";
  set_fields(grok(grok_expr, to_string($message.message)));
end

###########

rule "parse scenario"
when
  contains(to_string($message.message), "Scenario")
then
  let expr = grok("(?s):%{SPACE}%{GREEDYDATA:extra_info}Scenario:%{GREEDYDATA:json}", to_string($message.message));
  let sc = parse_json(to_string(expr["json"]));
  set_fields(expr);
  set_field("json", to_string(expr["json"]));
  set_fields(to_map(sc));
end

###########
